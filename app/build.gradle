plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.50'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.50'
}
repositories {
    google()
    jcenter()
    mavenCentral()
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'

ext {
    serialization_version = "0.13.0"
}


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId 'org.jetbrains.kotlin.mpp_app_android'
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName '1.0'
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
}

kotlin {
    android("android")
    // This is for iPhone emulator
    // Switch here to iosArm64 (or iosArm32) to build library for iPhone device
    iosX64("ios") {
        binaries {
            framework()
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
                implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.50"
                implementation deps.ktor.commonCore
                implementation deps.ktor.commonJson
            }
        }
        commonTest {
            dependencies {
        		implementation kotlin('test-common')
        		implementation kotlin('test-annotations-common')
                implementation deps.ktor.commonTest
            }
        }
        androidMain {
            dependencies {
                implementation kotlin('stdlib')
                implementation 'androidx.appcompat:appcompat:1.1.0'
                implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
                def recyclerview_version = "1.0.0"
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2'

                implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
                implementation deps.ktor.androidCore
                implementation deps.ktor.jvmJson

            }
        }
        androidTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
                implementation deps.ktor.jvmTest
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.2'
            }
        }
        iosMain {
            dependencies {
                implementation kotlin('test')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serialization_version"
                implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core-native:1.3.2'
                implementation deps.ktor.ios
                implementation deps.ktor.iosCore
                implementation deps.ktor.iosJson
                implementation deps.ktor.iosTest
            }
        }
        iosTest {
        }
    }
}

// This task attaches native framework built from ios module to Xcode project
// (see iosApp directory). Don't run this task directly,
// Xcode runs this task itself during its build process.
// Before opening the project from iosApp directory in Xcode,
// make sure all Gradle infrastructure exists (gradle.wrapper, gradlew).
task copyFramework {
    def buildType = project.findProperty('kotlin.build.type') ?: 'DEBUG'
    def target = project.findProperty('kotlin.target') ?: 'ios'
    dependsOn kotlin.targets."$target".binaries.getFramework(buildType).linkTask

    doLast {
        def srcFile = kotlin.targets."$target".binaries.getFramework(buildType).outputFile
        def targetDir = getProperty('configuration.build.dir')
        copy {
            from srcFile.parent
            into targetDir
            include 'app.framework/**'
            include 'app.framework.dSYM'
        }
    }
}